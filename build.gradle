plugins {
    id 'fabric-loom' version '1.6-SNAPSHOT'
    id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_21
targetCompatibility = JavaVersion.VERSION_21

archivesBaseName = "${project.name}-mc${project.minecraft_version}"
version = project.mod_version
group = project.maven_group

repositories {
    mavenCentral()
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.bluecolored.de/releases' }
    maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
    maven { url = "https://repo.mikeprimm.com/" }
    maven { url "https://maven.nucleoid.xyz/" }
    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

loom {
    log4jConfigs.from(file("log4j-dev.xml"))
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
    modImplementation "me.lucko:fabric-permissions-api:${project.lucko_permissions_version}"
    modImplementation "eu.pb4:sgui:${project.sgui_version}"
    modImplementation "eu.pb4:placeholder-api:${project.papi_version}"
    compileOnly "us.dynmap:DynmapCoreAPI:${project.dynmap_api_version}"
    compileOnly "de.bluecolored.bluemap:BlueMapAPI:${project.bluemap_api_version}"
    modCompileOnly "maven.modrinth:styled-chat:${project.styled_chat_version}"

    compileOnly "xyz.jpenilla:squaremap-api:${project.squaremap_api_version}"
}

processResources {
    inputs.property "version", project.version

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}"}
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
}

javadoc {
    source = sourceSets.main.allJava
}
